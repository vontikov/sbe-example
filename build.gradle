plugins {
  id 'application'
  id 'eclipse'
  id 'idea'
  id "com.github.vontikov.sbe-generator-plugin" version "0.0.9"
}

dependencies {
  // SBE codecs dependency
  implementation 'org.agrona:agrona:1.21.1'
}

sbeGenerator {
  src {
    dir = 'src/main/resources/schemas'
    // Include parent schema only
    includes = ['schema.xml']
  }

  // Java codecs dir
  javaCodecsDir = 'src/generated/java'

  // SBE options: see https://github.com/real-logic/simple-binary-encoding/wiki/Sbe-Tool-Guide
  javaOptions = [
    'sbe.xinclude.aware': 'true',
  ]
}

sourceSets {
  main {
    java {
      // default Gradle layout
      srcDirs = ['src/main/java/']
      // generated SBE stubs
      srcDir 'src/generated/java'
    }
  }
}

configurations {
  extendedImplementation.extendsFrom implementation
}

task fatJar(type: Jar) {
  group 'Build'
  description 'Assembles an executable jar.'
  manifest {
    attributes 'Main-Class': 'uk.co.real_logic.sbe.examples.ExampleUsingGeneratedStub'
  }
  archiveClassifier = "all"
  from {
    configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
  }
  with jar
}

defaultTasks 'clean', 'sbeGenerateJavaCodecs', 'fatJar'
